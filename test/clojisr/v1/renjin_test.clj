(ns clojisr.v1.renjin-test
  (:require [notespace.v1.note :as note
             :refer [note note-void note-md note-as-md note-hiccup note-as-hiccup]]))

(note-md "# Renjin interop")

(note-md "[Renjin](https://www.renjin.org/) is an implementation of the R language for the JVM. It supports the whole of the base R language, as well many R packages. For most packages, Renjin's support is partial and some tests fail. See the details [here](https://packages.renjin.org/).")

(note-md "Clojisr has now basic, experimental, support for Renjin as a backend. This tutorial shows some example usage.")

(note-void :setup)

(note-md "## Setup")

(note-void
 (require '[clojisr.v1.renjin :as renjin]
          '[clojisr.v1.r :as r :refer [r eval-r->java r->java java->r java->clj java->naive-clj clj->java r->clj clj->r ->code r+ colon]]
          '[clojisr.v1.require :refer [require-r]]
          '[tech.ml.dataset :as dataset]
          '[notespace.v1.util :refer [check]]
          '[clojisr.v1.applications.plotting :refer [plotting-function->svg]]))

(note-md "If we `require`d `clojisr.v1.renjin` first, then the default session-type would be `:renjin`. But since we might be loading this namespace after doing some other things, let us make sure that we are using `:renjin`:")

(note-void
 (renjin/set-as-default!)
 (r/discard-all-sessions))

(note-void :basic-examples)

(note-md "## Basic examples")

(note
 (require-r '[base]
            '[stats]))

(note
 (r ['+ 1 2]))

(note
 (r.stats/median [1 2 4]))

(note-md "From plain clojure data to an R dataframe:")

(note
 (-> {:x [1 2 3]
      :y [4 5 6]}
     r.base/data-frame))

(note
 (-> {:x [1 2 3]
      :y [4 5 6]}
     r.base/data-frame
     r.base/rowMeans))

(note-md "From a tech.ml.dataset dataset to an R dataframe:")

(note
 (-> {:x [1 2 3]
      :y [4 5 6]}
     dataset/name-values-seq->dataset
     r.base/data-frame))

(note
 (-> {:x [1 2 3]
      :y [4 5 6]}
     dataset/name-values-seq->dataset
     r.base/data-frame
     r.base/rowMeans))

(note-void :linear-regression)
(note-md "## Linear regression")

(note
 (let [xs     (repeatedly 99 rand)
       noises (repeatedly 99 rand)
       ys     (map (fn [x noise]
                     (+ (* x -3)
                        2
                        noise))
                   xs
                   noises)
       df     (r.base/data-frame
               :x xs
               :y ys)
       fit    (r.stats/lm '[tilde y x]
                          :data df)]
   (r.base/summary fit)))


(note-void :plotting)
(note-md "## Plotting")

(note-void
 (require-r '[graphics]))

(note-as-hiccup
 (plotting-function->svg
  (fn []
    (->> (repeatedly 999 rand)
         (map (fn [x] (* x x)))
         (r.graphics/hist
          :main "histogram"
          :xlab "x"
          :bins 100)))))

(note/render-this-ns!)
